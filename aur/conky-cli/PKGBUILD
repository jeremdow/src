# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Karol Cichy <slothck@gmail.com>

pkgname=conky-cli
pkgver=1.8.1
pkgrel=2
pkgdesc="Conky command line, without X11 dependencies"
arch=('i686' 'x86_64')
url="http://conky.sourceforge.net/"
license="custom"
provides=('conky')
conflicts=('conky')
depends=('ncurses')
source=("http://downloads.sourceforge.net/conky/conky-$pkgver.tar.gz")
md5sums=('9b330f7d001cbd9f818e485defc19029')

build() {
  cd "$srcdir/conky-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-lua --disable-x11 \
      --disable-double-buffer --disable-xdamage --disable-own-window \
      --disable-xft --disable-hddtemp --disable-portmon

  make
}

package() {
  cd "$srcdir/conky-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/conky/LICENSE
}

